apply plugin: 'com.android.application'

android {
    compileSdk 35
    compileOptions.encoding = 'UTF-8'

    defaultConfig {
        applicationId "io.github.ysdede.opencameraomt"
        minSdkVersion 21
        targetSdkVersion 35
        //compileSdkVersion 31 // needed to support appcompat:1.4.0 (which we need for emoji policy support, and not yet ready to target SDK 30)

        testApplicationId "net.sourceforge.opencamera.test"
        //testInstrumentationRunner "android.test.InstrumentationTestRunner"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        // OMT: Only build for arm64-v8a (our native libs are pre-built for this ABI)
        ndk {
            abiFilters 'arm64-v8a'
        }
    }

    // Load signing properties from local.properties (gitignored)
    def keystorePropertiesFile = rootProject.file("local.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    signingConfigs {
        release {
            if (keystoreProperties['RELEASE_STORE_FILE']) {
                storeFile file(keystoreProperties['RELEASE_STORE_FILE'])
                storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
                keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
                keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }


    // needed to use android.test package (ActivityInstrumentationTestCase2 etc) when targetting sdk 28 (Android 9) -
    // see https://developer.android.com/training/testing/set-up-project
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'net.sourceforge.opencamera'
    buildFeatures {
    }
    
    // OMT: CMake configuration for native JNI bridge
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }
    
    ndkVersion "27.0.12077973"
    //useLibrary 'android.test.mock'

    // Custom APK naming
    applicationVariants.all { variant ->
        variant.outputs.all {
            def buildType = variant.buildType.name
            if (buildType == "release") {
                outputFileName = "OpenCameraOMT.apk"
            } else {
                outputFileName = "OpenCameraOMT-${buildType}.apk"
            }
        }
    }
}

dependencies {
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'

    //implementation 'androidx.activity:activity:1.9.3' // needed for EdgeToEdge.enable(this)

    // appcompat version must be 1.4.0 or later to satisfy emoji policy!
    implementation 'androidx.appcompat:appcompat:1.7.1'

    // needed to fix errors since upgrading to appcompat:1.7.0, see https://stackoverflow.com/questions/75263047/duplicate-class-in-kotlin-android
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.9.0"))

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'androidx.exifinterface:exifinterface:1.4.1'

    testImplementation 'junit:junit:4.13.2'

    // newer AndroidJUnit4 InstrumentedTest
    androidTestImplementation "androidx.test:runner:1.7.0"
    androidTestImplementation "androidx.test:rules:1.7.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.7.0"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
